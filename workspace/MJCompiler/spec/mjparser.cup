package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.etf.pp1.symboltable.concepts.*;

parser code {:
	public boolean errorDetected;
	Logger log = Logger.getLogger(getClass());
   
   	public static class SimpleType
   	{
   		public SimpleType(Struct s) {this.s = s;}
   		public Struct s;
   		public rs.etf.pp1.symboltable.concepts.Obj obj = null;
   		public Integer num; Character character; Boolean bool;
   	}
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
    	errorDetected = true;
        //report_error("\nSyntax error", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
    	errorDetected = true;
        report_fatal_error("Fatal error, can't continue parsing", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with {: errorDetected = false; :}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, LBRACE, RBRACE, SEMI, DOT, LPAREN, RPAREN, LBRACKET, RBRACKET, EQUAL, RETURN, PLUS, MINUS, COMMA;
terminal READ, PRINT, VOID;
terminal Integer NUMBER;
terminal Boolean BOOL;
terminal Character CHAR;
terminal String IDENT;
terminal IF, ELSE, AND, OR;
terminal String EQ, NEQ, GRT, GRE, LSS, LSSE;
terminal CONST, CLASS, RECORD, NEW;
terminal DO, WHILE, BREAK, CONTINUE;
terminal STAR, SLASH, PERCENT;
terminal EXTENDS, SUPER;

nonterminal Declarations, Declaration, GlobalMethods;
nonterminal VarDeclarations, VarDeclarationsList, ConstDeclarations, ClassDeclaration, RecordDeclaration; 
nonterminal MethodList, FormParams, FormParamList, FormParam, OptionalVarDeclarations;
nonterminal RecordContent, RecordName, ClassContent, ClassMethods, Constructor, ConstructorName;
nonterminal ClassVarDeclarations, ClassVarDeclarationsList, ClassVar, ClassVarList;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ClassName, Extends;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, MethodType;

nonterminal rs.ac.bg.etf.pp1.MJParser.SimpleType Val;
nonterminal Var, VarList, ConstVarList;
nonterminal StatementList, Statement, Statements, SingleStatement;

nonterminal DesignatorStatement;
nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorList, DesignatorName;

nonterminal SuperCall;
nonterminal PrintStmt;
nonterminal ActualPars;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr, AssignExpr, Term, TermList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondTerm, CondFact;
nonterminal MulOp, AddOp, RelOp, AssignOp;

nonterminal DO_NT;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodTypeName, MethodDecl;
precedence left ELSE;

Program ::= (Program) PROG ProgName:p Declarations LBRACE GlobalMethods RBRACE;

ProgName ::= (ProgName) IDENT:progName;

Declarations ::= Declarations Declaration
	| /*epsilon*/
	;

Declaration ::= (Declarations_VarDecl) VarDeclarations
			| (Declarations_ConstDecl) ConstDeclarations
			| (Declarations_ClassDecl) ClassDeclaration
			| (Declarations_RecordDecl) RecordDeclaration
			;

ConstDeclarations ::= (Const_Declarations) CONST Type ConstVarList SEMI
	//| error SEMI:l
	//{:  parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
	;
ConstVarList ::= (ConstDecl_Single) IDENT:varName EQUAL Val
	| (ConstDecl_List) IDENT:varName EQUAL Val COMMA ConstVarList
	| error:l
	{:  parser.report_error("Error: invalid constant declaration in line " + lleft, null); :}
;

VarDeclarations ::= (Var_Declarations) Type VarList SEMI
	| (VarDecl_Error) error SEMI:l
	{: parser.report_error("Error: invalid declaration" + lleft, null); :}
	;

Val ::= (Val_Num) NUMBER
	| (Val_Char) CHAR
	| (Val_Bool) BOOL
	;
Var ::= (Var_) IDENT:varName
	| (Var_Array) IDENT:varName LBRACKET RBRACKET
	| error:l
	{: parser.report_error("Error: invalid variable declaration at line " + lleft, null); :}
	;
Type ::= (Type) IDENT:typeName;

VarList ::= (Var_List) VarList COMMA Var
		| (OneVar) Var
		;

ClassName ::= (ClassName) IDENT:name;

ClassDeclaration ::= (Class_NoParent_Declaration) CLASS ClassName LBRACE ClassContent RBRACE
	| (Class_Parent_Declaration) CLASS ClassName Extends:parentObj LBRACE ClassContent RBRACE
		;


Extends ::= (Extends_Parent) EXTENDS IDENT:parentName
	| error:l
	{: parser.report_error("Error: invalid extends declaration at line " + lleft, null); :}
	;

ClassContent ::= ClassMethods
	| ClassVarDeclarationsList ClassMethods
	| ClassVarDeclarationsList
	| /*epsilon*/
	;

ClassVarDeclarationsList ::= ClassVarDeclarations ClassVarDeclarationsList
	| ClassVarDeclarations
	| error:l
	{: parser.report_error("Error: invalid class attribute declarations at line " + lleft, null); :}
	;

ClassVarDeclarations ::= (ClassVarDecl) Type ClassVarList SEMI;

ClassVarList ::= ClassVar COMMA ClassVarList
		| ClassVar
		| error:l
		{: parser.report_error("Error: invalid class attribute declaration at line " + lleft, null); :}
		;
ClassVar ::= (Class_Var) IDENT:varName
	| (Class_Var_Arr) IDENT:varName LBRACKET RBRACKET
	;

ClassMethods ::= LBRACE Constructor MethodList RBRACE
	| LBRACE MethodList RBRACE
	| LBRACE Constructor RBRACE
	| LBRACE RBRACE
	;
	
Constructor ::= (Constructor) ConstructorName LPAREN RPAREN OptionalVarDeclarations Statements;	
ConstructorName ::= (ConstructorName) IDENT:name;
		
RecordDeclaration ::= (RecordDeclaration) RECORD RecordName LBRACE RecordContent RBRACE;

RecordName ::= (RecordName) IDENT:name;

RecordContent ::= RecordContent VarDeclarations
	| /*epsilon*/
	;

GlobalMethods ::= (Global_Methods) MethodList
	| /*epsilon*/
	;

MethodList ::= MethodList MethodDecl
	| MethodDecl
	;

MethodDecl ::= (Method_Decl) MethodTypeName LPAREN FormParams RPAREN OptionalVarDeclarations Statements;

MethodTypeName ::= (MethodTypeName) MethodType:methodType IDENT:methodName;

MethodType ::= (Method_Type) Type:typeName
			| (VoidType) VOID
			;

OptionalVarDeclarations ::= (Optional_VarDeclarations) VarDeclarations OptionalVarDeclarations
						| (Optional_NoVarDeclarations) /*epsilon*/
						;

FormParams ::= (FormParams_List) FormParamList
		| (NoFormParam) /*epsilon*/
		;
		
FormParamList ::= (FormParam_List) FormParamList COMMA FormParam
		| (SingleFormalParam) FormParam
		;

FormParam ::= (Form_Param) Type:typename IDENT:varname
	| (Form_Param_Arr) Type:typename IDENT:varname LBRACKET RBRACKET
	| error:l
	{: parser.report_error("Error: invalid parameter at line " + lleft, null); :}
	;

StatementList ::= (Statement_List) StatementList Statement
				|
				(NoStmt) /* epsilon */
				;

Statement ::= SingleStatement
	| Statements
	;

Statements ::= LBRACE StatementList RBRACE;
				
SingleStatement ::= (Statement_Designator) DesignatorStatement SEMI
			| (IfStmt) IF LPAREN Condition RPAREN Statement
			| (IfElseStmt) IF LPAREN Condition RPAREN Statement ELSE Statement
			| (DoWhileStmt) DO_NT Statements WHILE LPAREN Condition RPAREN SEMI
			| (BreakStmt) BREAK SEMI
			| (ContinueStmt) CONTINUE SEMI
			| (ReturnStmt) RETURN SEMI
			| (ReturnExprStmt) RETURN Expr SEMI
			| (ReadStmt) READ LPAREN Designator RPAREN SEMI
			| (Print_Stmt) PrintStmt SEMI
			;
DO_NT ::= (DO_NT) DO; 

PrintStmt ::= (PrintStmt_) PRINT LPAREN Expr RPAREN
	| (PrintStmt_Num) PRINT LPAREN Expr COMMA NUMBER RPAREN
	;

DesignatorStatement ::= (Assignment) Designator AssignOp AssignExpr
					| (FuncCall) Designator LPAREN ActualPars RPAREN
					| (Super_Call_Stmt) SUPER SuperCall LPAREN ActualPars RPAREN
					| (PostIncrement) Designator PLUS PLUS 
					| (PostDecrement) Designator MINUS MINUS
					; 

SuperCall ::= (Super_Call)/*epsilon*/;

Condition ::= (Condition_CondTerm) CondTerm
	|(Condition_List) Condition OR CondTerm
	| error:l
	{: parser.report_error("Error: invalid condition at line " + lleft, null); :}
	;
	
CondTerm ::= (CondTerm_CondFact) CondFact
	| (CondTerm_List) CondTerm AND CondFact
	;

CondFact ::= (CondFact_Expr) Expr 
	| (CondFact_Relop) Expr RelOp Expr
	;

AssignExpr ::= (Assign_Expr) Expr
	| error:l
	{: parser.report_error("Error: invalid assignment at line " + lleft, null); :}
	;

Expr ::= (Expr_Pos) TermList
	| (Expr_Neg) MINUS TermList
	;
	
TermList ::= (TermList_Term) Term
	| (TermList_AddOp) Term AddOp TermList
	;

Term ::= (Term_MulOp) Factor MulOp Term
	| (Term_Fact) Factor
	;

Factor ::= (Factor_Designator) Designator 
	| (Factor_FunCall) Designator LPAREN ActualPars RPAREN
	| (Factor_Super) SUPER LPAREN ActualPars RPAREN
	| (Factor_ConstVal) Val													
	| (Factor_New) NEW Type 
	| (Factor_New_Arr) NEW Type LBRACKET Expr RBRACKET
	| (Factor_ParenExpr) LPAREN Expr RPAREN
	;
	
Designator ::= (Designator) DesignatorName:name DesignatorList
;

DesignatorName ::= (Designator_Name) IDENT:name
	| (DesignatorName_Arr) IDENT:name LBRACKET Expr RBRACKET
	;

DesignatorList ::= (DesignatorList_Arr) DesignatorList DOT IDENT:name LBRACKET Expr RBRACKET 
	| (DesignatorList_Field) DesignatorList DOT IDENT:name 
	| /*epsilon*/
	;


ActualPars ::= (ActualPars_Expr) Expr
	| (ActualPars_List) ActualPars COMMA Expr
	| /* epsilon*/
	;

AssignOp ::= EQUAL;

AddOp ::= PLUS
	| MINUS
	;

MulOp ::= STAR
	| SLASH
	| PERCENT
	;

RelOp ::= (RelOp_EQ) EQ:t
	| (RelOp_NEQ) NEQ:t
	| GRT:t
	| GRE:t
	| LSS:t
	| LSSE:t
	;
	
	
	


